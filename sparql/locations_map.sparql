PREFIX wb: <https://medievalcharterskg.wikibase.cloud/entity/>
PREFIX wbt: <https://medievalcharterskg.wikibase.cloud/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#defaultView:Map
SELECT ?personLabel ?entityLabel ?coord_raw ?label ?rgb   WHERE {
  {
    ?person wbt:P55 ?entity.
    ?entity wbt:P2 ?wdEntity.
    BIND("00a2ff" AS ?rgb) 
    BIND("Residence" AS ?label) 
  } UNION {
    ?person wbt:P53 ?entity.
    ?entity wbt:P2 ?wdEntity.
    BIND("ff0f00" AS ?rgb)  
    BIND("Work location" AS ?label) 
  } UNION {
    ?person wbt:P65 ?office.
    ?office wbt:P64 ?entity.
    ?entity wbt:P2 ?wdEntity.
    BIND("11a400" AS ?rgb)
    BIND("Holds public office with jurisdiction in" AS ?label) 
  }
  
  # Etiquetas locales
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es,en" .
    ?person rdfs:label ?personLabel .
    ?entity rdfs:label ?entityLabel .
  }
  
  # Extraemos solo el ID de Wikidata (QXXXX) de la URI completa
  BIND(IRI(CONCAT("http://www.wikidata.org/entity/", ?wdEntity)) AS ?wdItem)
  
  # Consulta federada optimizada
  OPTIONAL {
    SERVICE <https://query.wikidata.org/sparql> {
      { SELECT ?wdItem ?coord_raw  WHERE {
        ?wdItem wdt:P625 ?coord_raw  .
      } }
    }
  }
  
}
