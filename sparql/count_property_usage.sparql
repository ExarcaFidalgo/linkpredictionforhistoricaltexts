#Counts usage of each property with percentage
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <https://medievalcharterskg.wikibase.cloud/prop/direct/>
PREFIX p: <https://medievalcharterskg.wikibase.cloud/prop/>

SELECT ?propertyLabel (COUNT(*) AS ?numberOfUsages) 
       (100 * COUNT(*) / ?total AS ?percentage)
WHERE {
  {
    SELECT (COUNT(*) AS ?total) WHERE {
      ?s ?p ?o .
      ?property wikibase:directClaim ?p .
    }
  }
  
  ?s ?p ?o .
  ?property wikibase:directClaim ?p .
  
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" .
    ?property rdfs:label ?propertyLabel .
  }
}
GROUP BY ?propertyLabel ?total
ORDER BY DESC(?numberOfUsages)
