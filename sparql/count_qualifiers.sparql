PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <https://medievalcharterskg.wikibase.cloud/prop/direct/>
PREFIX p: <https://medievalcharterskg.wikibase.cloud/prop/>

SELECT ?propertyLabel ?total (COUNT(*) AS ?numberOfUsages)
       (100 * COUNT(*) / ?total AS ?percentage)
WHERE {
  {
    SELECT (COUNT(*) AS ?total) WHERE {
      # Match any triple where the predicate is a qualifier predicate
      ?statementNode ?pq ?qualifierValue .
      # Link the qualifier predicate back to the property item
      ?property wikibase:qualifier ?pq .
    }
  }

  # Main pattern to count usages of each specific property as a qualifier
  ?statementNode ?pq ?qualifierValue .
  ?property wikibase:qualifier ?pq .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" .
    ?property rdfs:label ?propertyLabel .
  }
}
GROUP BY ?propertyLabel ?total
ORDER BY DESC(?numberOfUsages)
